import com.osacky.flank.gradle.Device

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.osacky.fladle'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.osacky.flank.gradle.sample"
        minSdkVersion 23
        targetSdkVersion 28
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

fladle {
    serviceAccountCredentials("${project.file("flank-gradle-5cf02dc90531.json")}")
    // Project Id is not needed if serviceAccountCredentials are set.
//    projectId("flank-gradle")
    useOrchestrator = true
    environmentVariables = [
            "clearPackageData": "true"
    ]
    testTargets = [
            "class com.osacky.flank.gradle.sample.ExampleInstrumentedTest#seeView"
    ]
    devices = [
            new Device("NexusLowRes", 28, null, null),
            new Device("Nexus5", 23, null, null)
    ]
    smartFlankGcsPath = "gs://test-lab-yr9w6qsdvy45q-iurp80dm95h8a/flank/test_app_android.xml"
    configs {
        oranges {
            useOrchestrator = false
            testTargets = [
                    "class com.osacky.flank.gradle.sample.ExampleInstrumentedTest#runAndFail"
            ]
            flakyTestAttempts = 3
        }
    }
    flakyTestAttempts 1
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation('android.arch.navigation:navigation-fragment-ktx:1.0.0'){
        exclude group: 'com.android.support'
    }
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}

