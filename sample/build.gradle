apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'com.osacky.fladle'

android {
    compileSdkVersion 29
    defaultConfig {
        applicationId "com.osacky.flank.gradle.sample"
        minSdkVersion 23
        targetSdkVersion 29
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

fladle {
    async = true
    flankVersion = "21.+"
    serviceAccountCredentials = project.layout.projectDirectory.file("flank-gradle-5cf02dc90531.json")
    // Project Id is not needed if serviceAccountCredentials are set.
//    projectId("flank-gradle")
    useOrchestrator = true
    environmentVariables = [
            "clearPackageData": "true"
    ]
    testTargets = [
            "class com.osacky.flank.gradle.sample.ExampleInstrumentedTest#seeView"
    ]
    devices = [
            [ "model": "Pixel2", "version": "26" ],
            [ "model": "Nexus5", "version": "23" ]
    ]
    smartFlankGcsPath = "gs://test-lab-yr9w6qsdvy45q-iurp80dm95h8a/flank/test_app_android.xml"
    configs {
        oranges {
            // Cannot use = syntax: https://github.com/gradle/gradle/issues/9987
            useOrchestrator.set(false)
            testTargets.set(project.provider { [
                    "class com.osacky.flank.gradle.sample.ExampleInstrumentedTest#runAndFail"
            ] })
            flakyTestAttempts.set(3)
        }
        additionalTests {
            useOrchestrator.set(false)
            testTargets.set(project.provider { [
                "class com.osacky.flank.gradle.sample.ExampleInstrumentedTest#runAndFail"
            ] })
            flakyTestAttempts.set(3)
            additionalTestApks.value(project.provider { [
                "app: ../main/app/build/output/apk/debug/app.apk",
                "test: ../main/app/build/output/apk/androidTest/debug/app-test.apk",
                "app: ../sample/app/build/output/apk/debug/sample-app.apk",
                "test: ../sample/app/build/output/apk/androidTest/debug/sample-app-test.apk",
                "test: ../feature/room/build/output/apk/androidTest/debug/feature-room-test.apk",
                "test: ../library/databases/build/output/apk/androidTest/debug/sample-databases-test.apk"
            ]})
        }
        perfTests {
            devices.set([[ "model" : "Nexus5", "version" : "28" ], ["model" : "Nexus5", "version": "28"]])
            testTargets.set([
                    "class com.sample.MyPerformanceTest"
            ])
        }
        regressionTests {
            devices.set([[ "model" : "Nexus5LowRes", "version" : "28" ]])
            testTargets.set([
                    "class com.sample.MyRegressionTest"
            ])
        }
    }
    flakyTestAttempts = 1
}

fulladleModuleConfig {
    debugApk.set("${rootProject.file("dummy_app.apk")}")
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    implementation 'androidx.appcompat:appcompat:1.1.0'
    implementation('androidx.navigation:navigation-fragment-ktx:2.3.0')
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    testImplementation 'junit:junit:4.13'
    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:rules:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}

