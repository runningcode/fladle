plugins {
    id 'com.android.application'
    id 'com.osacky.fladle'
}

android {
    compileSdk 33
    namespace = "com.osacky.flank.gradle.sample"
    defaultConfig {
        applicationId "com.osacky.flank.gradle.sample"
        minSdk 23
        targetSdk 33
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }
}

fladle {
    serviceAccountCredentials = project.layout.projectDirectory.file("flank-gradle-5cf02dc90531.json")
    useOrchestrator = true
    environmentVariables = [
            "clearPackageData": "true",
            "listener": "com.osacky.flank.sample.Listener"
    ]
    testTargets = [
            "class com.osacky.flank.gradle.sample.ExampleInstrumentedTest#seeView"
    ]
    devices = [
            [ "model": "SmallPhone.arm", "version": "26" ],
            [ "model": "MediumPhone.arm", "version": "33" ]
    ]
    smartFlankGcsPath = "gs://test-lab-yr9w6qsdvy45q-iurp80dm95h8a/flank/test_app_android.xml"
    configs {
        oranges {
            useOrchestrator.set(false)
            testTargets.set(project.provider { [
                    "class com.osacky.flank.gradle.sample.ExampleInstrumentedTest#runAndFail"
            ] })
            flakyTestAttempts.set(3)
        }
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation("androidx.navigation:navigation-fragment-ktx:2.3.0")
    implementation 'androidx.constraintlayout:constraintlayout:2.0.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test:rules:1.5.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}

