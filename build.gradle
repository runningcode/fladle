// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.5.0'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.1.3'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.38.0"
    id "com.osacky.fulladle"
    id "org.jmailen.kotlinter" version "3.4.0"
}

apply from: 'gradle/scans-quality-check.gradle'

kotlinter {
  indentSize = 2
}

allprojects {
    apply plugin: "org.jmailen.kotlinter"

    kotlinter {
        indentSize = 2
    }
}

fladle {
    serviceAccountCredentials = project.layout.projectDirectory.file("sample/flank-gradle-5cf02dc90531.json")
    environmentVariables = [
        "var1-key" : "var1-value"
    ]
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}

tasks.wrapper.configure {
    gradleVersion = '7.0.2'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    // Example 1: reject all non stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
