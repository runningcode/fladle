// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    ext.kotlin_version = '1.4.10'
    repositories {
        google()
        mavenCentral()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:4.0.1'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id "com.github.ben-manes.versions" version "0.33.0"
    id "com.osacky.fulladle"
    id "org.jmailen.kotlinter" version "3.2.0"
}

apply from: 'gradle/scans-quality-check.gradle'

kotlinter {
  indentSize = 2
}

allprojects {
    repositories {
        google()
        mavenCentral()
        // jcenter is required for "org.jetbrains.trove4j:trove4j:20160824."
        jcenter() {
            content {
                includeVersion 'org.jetbrains.trove4j', 'trove4j', '20160824'
            }
        }
    }
    apply plugin: "org.jmailen.kotlinter"

    kotlinter {
        indentSize = 2
    }
}

fladle {
    serviceAccountCredentials = project.layout.projectDirectory.file("sample/flank-gradle-5cf02dc90531.json")
}

buildScan {
    termsOfServiceUrl = 'https://gradle.com/terms-of-service'
    termsOfServiceAgree = 'yes'
    publishAlways()
}

tasks.wrapper.configure {
    distributionType = Wrapper.DistributionType.ALL
    gradleVersion = '6.7'
}

def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { it -> version.toUpperCase().contains(it) }
    def regex = /^[0-9,.v-]+(-r)?$/
    return !stableKeyword && !(version ==~ regex)
}

dependencyUpdates {
    // Example 1: reject all non stable versions
    rejectVersionIf {
        isNonStable(it.candidate.version)
    }
}
